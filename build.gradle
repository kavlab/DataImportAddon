buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath 'io.jmix.gradle:jmix-gradle-plugin:2.5.0'
    }
}

group = 'ru.kavlab'
version = '0.9.1'

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'io.jmix'

    repositories {
        mavenCentral()
        maven {
            url 'https://global.repo.jmix.io/repository/public'
        }
    }

    dependencies {
        implementation 'org.apache.olingo:odata-commons-api:4.9.0'
        implementation 'org.apache.olingo:odata-commons-core:4.9.0'
        implementation 'org.apache.olingo:odata-client-api:4.9.0'
        implementation 'org.apache.olingo:odata-client-core:4.9.0'
    }

    jmix {
        bomVersion = '2.5.0'
        projectId = 'imp1c'
    }

    group = this.group
    version = this.version

    def props = new Properties()
    buildFile.withInputStream { props.load(it) }
    def subArchivesBaseName = props.getProperty('archivesBaseName')
    def archName = subArchivesBaseName.substring(1, subArchivesBaseName.length() - 1)

    java {
        withSourcesJar()
    }

    artifacts {
        archives sourcesJar
    }

    publishing {
        repositories {
            maven {
                url = 'https://myrepo/releases/'
                credentials {
                    username rootProject.hasProperty('uploadUser') ? rootProject['uploadUser'] : 'admin'
                    password rootProject.hasProperty('uploadPassword') ? rootProject['uploadPassword'] : 'admin'
                }
                allowInsecureProtocol = true
            }
        }
        publications {
            javaMaven(MavenPublication) {
                artifactId = archName
                from components.java
            }
        }
    }

    // remove after https://youtrack.jetbrains.com/issue/IDEA-227215 is fixed
    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }

    project.tasks.withType(JavaCompile).configureEach {
        options.compilerArgs.add("-parameters")
    }
}
